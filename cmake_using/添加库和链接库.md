# 添加库和链接库

- 在cmake中，如果要使用库，需要两步：**添加库**和**链接库**

  - **添加库**：**将源代码编译成库文件的形式**。
  - **链接库**：在**编译**可执行文件的过程中，**link**库文件。

- 在一个工程中，不可能把所有的源文件都写在一个目录下，通常是把不同功能的源文件放在不同的目录下，最外层只放置`main.cpp`、`CMakeLists.txt`还有一些其他配置文件。

  - **cmake**如何使用不同目录下面的源文件呢？
    - 将不同目录下面的源文件编译成库(添加库)，在上层的**CMakeLists.txt**中链接这个库，就可以在**上层源文件**中调用子目录中的源文件。

- 我们的文件结构如下所示：

  - ```
    ├── CMakeLists.txt
    ├── MathFunctions
    │   ├── CMakeLists.txt
    │   ├── MathFunctions.cpp
    │   ├── MathFunctions.h
    │   ├── mysqrt.cpp
    │   └── mysqrt.h
    ├── TestConfig.h.in
    └── test.cpp
    ```

  - 很显然，我们要在`test.cpp`中调用`MathFunctions`目录下面的源文件中的函数，因此我们需要把该目录下面的源文件编译成**库文件**。

    - **MathFunctions目录**给出了一个典型库文件的写法，对外展示一个**MathFunctions.cpp**和**MathFunctions.h**文件，如果要扩充这个库(比如增加加法函数，我们叫做扩充这个库的**细节**)，只需要增加**myadd.cpp**、**myadd.h**，并且声明在**MathFunctions.cpp**和**MathFunctions.h**文件即可。

- **MathFunctions.h**：

  - ```cpp
    #ifndef __MATHFUNCTIONS_H__
    #define __MATHFUNCTIONS_H__
    #include "mysqrt.h"
    
    namespace mathfunctions
    {
        double sqrt(double x);
    } // namespace mathfunctions
    
    #endif
    ```
    

- **MathFunctions.cpp**：

  - ```cpp
    #include "MathFunctions.h"
    
    double mathfunctions::sqrt(double x)
    {
        return mathfunctions::details::mysqrt(x);
    }
    ```

- **mysqrt.h**：

  - ```cpp
    #ifndef __MYSQRT_H__
    #define __MYSQRT_H__
    namespace mathfunctions
    {
        namespace details
        {
            double mysqrt(double x);        
        } // namespace details
        
    } // namespace mathfunctions
    
    #endif
    ```

- **mysqrt.cpp**：

  - ```cpp
    #include "mysqrt.h"
    #include <iostream>
    double mathfunctions::details::mysqrt(double x)
    {
        if (x <= 0)
        {
            return 0;
        }
    
        double result = x;
    
        // do ten iterations
        for (int i = 0; i < 10; ++i)
        {
            if (result <= 0)
            {
                result = 0.1;
            }
            double delta = x - (result * result);
            result = result + 0.5 * delta / result;
        }
        return result;
    }
    ```

- 对于上述源文件和头文件，我们的想法是编译成一个库：**MathFunctions**。

  - 有两种思路：

    - 掺杂编译：
      - 将所有的源文件混在一起编译，最终形成一个库。
    - 分开编译：
      - 将**mysqrt.cpp**编译成一个库：**MathMysqrt**，然后将**MathMysqrt**以及**MathFunctions.cpp**编译成库：**MathFunctions**。

  - 这里我们选择**分开编译**：

    - 首先，构建**MathMysqrt库**：

      - ```cmake
        add_library(MathMysqrt mysqrt.cpp)
        #添加MathMysqrt库，也就是编译这个源文件，生成库文件
        ```

    - 接着，我们构建**MathFunctions库**：

      - ```cmake
        add_library(MathFunctions MathFunctions.cpp)
        #添加MathFunctions库，也就是编译这个源文件，生成库文件
        
        add_library(MathMysqrt mysqrt.cpp)
        
        target_link_libraries(MathFunctions PRIVATE MathMysqrt)
        #编译MathFunctions的过程中，链接MathMysqrt库文件，这个链接过程设置为私有
        ```

    - 最后，在上层**CMakeLists.txt**文件中调用**MathFunctions**库文件：

      - 这里，我们要给上层调用者一个库的接口，通常，库所对应的头文件就是库的接口，即**MathFunctions.h**。

      - ```cmake
        cmake_minimum_required(VERSION 3.10)
        
        project(Test VERSION 1.0)
        
        set(CMAKE_CXX_STANDARD 11)
        set(CMAKE_CXX_STANDARD_REQUIRED True)
        
        configure_file(TestConfig.h.in TestConfig.h)
        
        add_subdirectory(MathFunctions)
        
        add_executable(Test test.cpp)
        
        target_link_libraries(Test PUBLIC MathFunctions)
        
        target_include_directories(Test PUBLIC 
                                   "${PROJECT_BINARY_DIR}"
                                   "${PROJECT_SOURCE_DIR}/MathFunctions"
        )
        ```

      - 关于`add_subdirectory(MathFunctions)`：

        - 给出要调用的库的路径，实际上会执行该路径下的**CMakeLists.txt**，也就是**编译出**要使用的库文件。

      - 关于`target_link_libraries(Test PUBLIC MathFunctions)`：

        - 在构建**Test**可执行文件的时候，公开链接**MathFunctions**这个库。

      - 关于`target_include_directories`，要找到**MathFunctions**库对外的**接口头文件**。

    - **test.cpp**：

      - ```cpp
        #include <iostream>
        #include "TestConfig.h"
        #include "MathFunctions.h"
        int main(int argc, char *argv[])
        {
            if (argc == 1)
            {
                std::cout << "Hello World" << std::endl;
            }
            else if (argc == 2)
            {
                std::string argv_1 = argv[1];
                if (argv_1 == "--version")
                {
                    std::cout << " Version " << Test_VERSION_MAJOR << "." << Test_VERSION_MINOR << std::endl;
                }
                else 
                {
                    double x = std::stod(argv_1);
                    std::cout << argv_1 << " sqrt is: " << mathfunctions::sqrt(x) << std::endl; 
                }
            }
            
        }
        ```

    - 构建信息：

      - ```shell
        [ 16%] Building CXX object MathFunctions/CMakeFiles/MathMysqrt.dir/mysqrt.cpp.o
        [ 33%] Linking CXX static library libMathMysqrt.a
        [ 33%] Built target MathMysqrt
        [ 50%] Building CXX object MathFunctions/CMakeFiles/MathFunctions.dir/MathFunctions.cpp.o
        [ 66%] Linking CXX static library libMathFunctions.a
        [ 66%] Built target MathFunctions
        Consolidate compiler generated dependencies of target Test
        [ 83%] Building CXX object CMakeFiles/Test.dir/test.cpp.o
        [100%] Linking CXX executable Test
        [100%] Built target Test
        ```

        - 可以看到，在构建**Test**可执行文件之前，先构建了**MathFunctions库**，在这之前还构建了**MathMysqrt库**。

    - 执行结果：

      - `./Test 10`

      - ```shell
        10 sqrt is: 3.16228

- 如果我们有权限选择**MathFunctions**中的**sqrt函数**调用**MathMysqrt**中的**mysqrt**或者**标准库**中的**sqrt**，那么我们就需要在**MathFunctions**中以及**MathFunctions**对应的**CMakeLists.txt**中都设置条件，以符合**条件编译**。

  - 当我们需要调用**MathMysqrt**中的**mysqrt**时，就需要编译**MathMysqrt**这个库文件。
  - 当我们需要调用**标准库**中的**sqrt**时，就不需要编译**MathMysqrt**这个库文件。

- **CMakeLists.txt**：

  - ```cmake
    add_library(MathFunctions MathFunctions.cpp)
    option(USE_MYMATH "Use my sqrt functions" ON)
    
    if (USE_MYMATH)
    target_compile_definitions(MathFunctions PRIVATE "USE MYMATH")
    
    add_library(MathMysqrt mysqrt.cpp)
    
    target_link_libraries(MathFunctions PRIVATE MathMysqrt)
    endif()
    ```

  - 关于**option**：

    - 使用**option**命令创建了一个**变量**(通常叫做**编译定义**)**USE_MYMATH**，这个变量被配置为**ON**。

  - 关于`target_compile_definitions`：

    - 将**USE_MYMATH**这个编译定义传递到**MathFunctions库**中(相当于**#define**声明**宏**)。

  - 如果**USE_MYMATH**这个编译定义被设置，那么将会编译**MathMysqrt库**，同时**MathFunctions库**由**MathMysqrt库**构建。

- **MathFunctions.cpp**：

  - ```cpp
    #include "MathFunctions.h"
    
    double mathfunctions::sqrt(double x)
    {
    #ifdef USE_MYMATH
        return mathfunctions::details::mysqrt(x);
    #else
        return std::sqrt(x);
    #endif
    }
    ```

- **MathFunctions.h**：

  - ```cpp
    #ifndef __MATHFUNCTIONS_H__
    #define __MATHFUNCTIONS_H__
    
    #ifdef USE_MYMATH
    #include "mysqrt.h"
    #else
    #include <cmath>
    #endif
    
    namespace mathfunctions
    {
        double sqrt(double x);
    } // namespace mathfunctions
    
    #endif
    ```

- 开启**USE_MYMATH**编译构建：

  - ```shell
    [ 33%] Built target MathMysqrt
    Consolidate compiler generated dependencies of target MathFunctions
    [ 66%] Built target MathFunctions
    Consolidate compiler generated dependencies of target Test
    [100%] Built target Test
    ```

    - 可以发现，此时编译了**MathMysqrt库文件**。

- 关闭**USE_MYMATH**编译构建：

  - ```shell
    Consolidate compiler generated dependencies of target MathFunctions
    [ 50%] Built target MathFunctions
    Consolidate compiler generated dependencies of target Test
    [100%] Built target Test
    ```

    - 可以发现，此时没有编译**MathMysqrt库**文件。

---

- 如何简化链接库的过程：

  - 目前的过程：

    - `add_subdirectory(MathFunctions)`，编译子目录，构建**MathFunctions**库(也就是执行子目录的**CMakeLists.txt**)。
    - `target_link_libraries(Test PUBLIC MathFunctions)`，链接库，在编译可执行文件**Test**的时候链接**MathFunctions**库。
    - `target_include_directories`，找到**MathFunctions**对外的**接口头文件**。

  - 简化的过程：

    - `add_subdirectory(MathFunctions)`，编译子目录，构建**MathFunctions**库(也就是执行子目录的**CMakeLists.txt**)。
    - `target_link_libraries(Test PUBLIC MathFunctions)`，链接库，在编译可执行文件**Test**的时候链接**MathFunctions**库。

  - 如果我们添加库之后还需要找到库文件对外的接口头文件，那么是一件**非常麻烦**的事情，并且对整个工程结构有很大的影响。

    - 因此，我们尝试在库中指明**库文件自己的对外接口头文件**的路径，这样在**顶层**在**链接库**的时候就知道**接口头文件**的路径了。

  - 使用**INTERFACE**关键字：

    - 在子目录(MathFunctions)的CMakeLists.txt中添加：
      - `target_include_directories(MathFunctions INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})`
      - 这句话可以自动帮助**库的调用者**找到库文件对外的接口头文件路径，而不需要库的调用者手动添加。
      - 同时，这句话并不影响**MathFunctions**的构建，因为**INTERFACE关键字不对当前库生效**。

  - **子目录的CMakeLists.txt**：

    - ```cmake
      add_library(MathFunctions MathFunctions.cpp)
      
      target_include_directories(MathFunctions INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})
      
      option(USE_MYMATH "Use my sqrt functions" OFF)
      
      if (USE_MYMATH)
      target_compile_definitions(MathFunctions PRIVATE "USE_MYMATH")
      
      add_library(MathMysqrt mysqrt.cpp)
      
      target_link_libraries(MathFunctions PRIVATE MathMysqrt)
      endif()
      ```

  - **顶层CMakeLists.txt**：

    - ```cmake
      cmake_minimum_required(VERSION 3.10)
      
      project(Test VERSION 1.0)
      
      set(CMAKE_CXX_STANDARD 11)
      set(CMAKE_CXX_STANDARD_REQUIRED True)
      
      configure_file(TestConfig.h.in TestConfig.h)
      
      add_subdirectory(MathFunctions)
      
      add_executable(Test test.cpp)
      
      target_link_libraries(Test PUBLIC MathFunctions)
      
      target_include_directories(Test PUBLIC 
                                 "${PROJECT_BINARY_DIR}"
      )
      ```

  - 构建结果：

    - ```shell
      [ 25%] Building CXX object MathFunctions/CMakeFiles/MathFunctions.dir/MathFunctions.cpp.o
      [ 50%] Linking CXX static library libMathFunctions.a
      [ 50%] Built target MathFunctions
      [ 75%] Building CXX object CMakeFiles/Test.dir/test.cpp.o
      [100%] Linking CXX executable Test
      [100%] Built target Test
      ```

      - 没有任何问题。

---

- 如何更高级的去制定编译标准？

  - **设置变量值**制定**C++ 11编译标准**：

    - ```cmake
      set(CMAKE_CXX_STANDARD 11)
      set(CMAKE_CXX_STANDARD_REQUIRED True)
      ```

      - 通过对上述两个变量设置值，进而指定编译标准。
      - 这样做有什么缺点呢？
        - 不好意思，我也不知道:face_with_head_bandage:。

  - **添加接口库**制定**C++ 11编译标准**：

    - 首先，在**顶层CMakeLists.txt**中添加接口库(**Interface Lib**)，并且为**接口库**添加**C++ 11**的编译标准。

      - ```cmake
        add_library(test_compiler_flags INTERFACE)
        target_compile_features(test_compiler_flags INTERFACE cxx_std_11)
        ```

      - 这里做了两件事情：

        - 添加一个库，关键字是**INTERFACE**，也就是**接口库**，接口库对于**顶层CMakeLists.txt**和**子目录中CMakeLists.txt**均可见。
        - 接下来，为这个接口库添加**编译特性**，可以认为这个接口库的一部分是**C++ 11标准库**。

    - 接下来，无论**顶层CMakeLists.txt**还是**子目录CMakeLists.txt**都链接该接口库，相当于都链接上了**C++ 11标准库**。

      - **顶层CMakeLists.txt**：

        - ```cmake
          target_link_libraries(Test PUBLIC MathFunctions test_compiler_flags)
          ```

      - **完整的顶层CMakeLists.txt**：

        - ```cmake
          cmake_minimum_required(VERSION 3.10)
          
          project(Test VERSION 1.0)
          
          add_library(test_compiler_flags INTERFACE)
          target_compile_features(test_compiler_flags INTERFACE cxx_std_11)
          
          configure_file(TestConfig.h.in TestConfig.h)
          
          add_subdirectory(MathFunctions)
          
          add_executable(Test test.cpp)
          
          target_link_libraries(Test PUBLIC MathFunctions test_compiler_flags)
          
          target_include_directories(Test PUBLIC 
                                     "${PROJECT_BINARY_DIR}"
          )
          ```

      - **MathFunctions/CMakeLists.txt**：

        - ```cmake
          target_link_libraries(MathMysqrt PUBLIC test_compiler_flags)
          #在编译MathMysqrt库的时候，需要链接接口库
          
          target_link_libraries(MathFunctions PUBLIC test_compiler_flags)
          #之后，在编译MathFunctions库的时候，需要链接接口库
          ```

      - **完整的MathFunctions/CMakeLists.txt**：

        - ```cmake
          add_library(MathFunctions MathFunctions.cpp)
          
          target_include_directories(MathFunctions INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})
          option(USE_MYMATH "Use my sqrt functions" ON)
          
          if (USE_MYMATH)
          target_compile_definitions(MathFunctions PRIVATE "USE_MYMATH")
          
          add_library(MathMysqrt mysqrt.cpp)
          #编译MathMysqrt库
          
          target_link_libraries(MathMysqrt PUBLIC test_compiler_flags)
          #MathMysqrt库链接test_compiler_flags接口库
          
          target_link_libraries(MathFunctions PRIVATE MathMysqrt)
          #MathFunctions库链接MathMysqrt库
          
          #if里面的语句都是可选的，根据USE_MYMATH这个编译定义决定
          
          endif()
          target_link_libraries(MathFunctions PUBLIC test_compiler_flags)
          #MathFunctions库链接test_compiler_flags库，这一步是必须得，因此写在if外面
          ```