# 系统自省

- **系统自省——system introspection**，对一些**依赖目标平台**的部分进行检查。

- 之前说过添加**编译定义**，我们通过为手动的开关**编译定义**，从而为**源代码**传递**宏定义**，通过在**源代码**中设置**条件编译**，即可实现功能的切换。

  - 编译定义：`option(USE_MYMATH "Use my math implementation" ON)`
  - 传递宏定义：`target_compile_definitions(MathFunctions PRIVATE "USE_MYMATH")`
    - 这样就可以在**MathFunctions**的相关文件中使用**USE_MYMATH**宏定义。
  - 但这是手动的，比如我知道目标平台中没有提供**log函数**，因此可以使用**编译定义**传递**宏定义**，通过**源代码**的**条件编译**，使用自己实现的**log函数**。

- 如何**自动检测**目标平台是否提供某些依赖，比如是否提供**log函数**？

  - 显而易见，使用**系统自省**功能......

- **System Introspection过程**：

  - 首先检测目标平台中有没有**提供依赖**：

    - 使用`check_cxx_source_compiles()`函数，在使用该函数之前需要包含所在模块`include(CheckCXXSourceCompiles)`

    - 该函数接收一个**main函数**(**main函数**应尽可能简短，并且含有**需要检测的依赖**)，同时接收一个**变量**，如果**main函数**编译成功，那么该变量为**True**，反之为**False**。

    - ```cmake
      check_cxx_source_compiles("
      	#include <cmath>
      	int main()
      	{
      		std::log(1.0);
              return 0;
      	}"
      	HAVE_LOG
      )
      ```

      - 如果里面的**main函数**编译成功，**HAVE_LOG**将被赋值为**Ture**，反之为**False**。

  - 如果目标平台能够提供依赖，那么向**构建源码**中提供**宏定义**：

    - ```cmake
      if(HAVE_LOG)
      	target_compile_definitions(MathMysqrt PROVATE "HAVE_LOG") #假设在MathMysqrt构建过程中调用log函数。
      endif()
      ```

- **子目录的CMakeLists.txt**：

  - ```cmake
    add_library(MathFunctions MathFunctions.cpp)
    
    
    option(USE_MYMATH "Use my sqrt functions" ON)
    
    if (USE_MYMATH)
        target_compile_definitions(MathFunctions PRIVATE "USE_MYMATH")
    
        add_library(MathMysqrt mysqrt.cpp)
    
        include(CheckCXXSourceCompiles)
        check_cxx_source_compiles("
        #include <cmath>
        int main()
        {
            std::log(1.0);
            return 0;
        }"
        HAVE_LOG
        )
        if(HAVE_LOG)
            target_compile_definitions(MathMysqrt PRIVATE "HAVE_LOG")
        endif()
    
        target_link_libraries(MathMysqrt PUBLIC test_compiler_flags)
    
        target_link_libraries(MathFunctions PRIVATE MathMysqrt)
    endif()
    
    target_link_libraries(MathFunctions PUBLIC test_compiler_flags)
    
    target_include_directories(MathFunctions INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})
    
    set(install_libs MathFunctions test_compiler_flags)
    set(install_includes MathFunctions.h)
    
    if (TARGET MathMysqrt)
        list(APPEND install_libs MathMysqrt)
    endif()
    
    install(TARGETS ${install_libs} DESTINATION lib)
    install(FILES ${install_includes} DESTINATION include)
    ```

- **mysqrt.cpp**：

  - ```cpp
    #include "mysqrt.h"
    
    double mathfunctions::details::mysqrt(double x)
    {
        #ifdef HAVE_LOG
        std::cout << "HAVE_LOG" << std::endl;
        #endif
        
        if (x <= 0)
        {
            return 0;
        }
    
        double result = x;
    
        // do ten iterations
        for (int i = 0; i < 10; ++i)
        {
            if (result <= 0)
            {
                result = 0.1;
            }
            double delta = x - (result * result);
            result = result + 0.5 * delta / result;
        }
        return result;
    }
    ```

- 构建结果：

  - ```shell
    HAVE_LOG
    20 sqrt is: 4.47214
    ```

- 注意事项：

  - 和**编译定义**一样，**系统自省**的结果也会保存在**cache**中，更改之后需要清空，再次构建生效。