# 安装

- 通常情况下，仅仅**构建**可执行文件是不够的，还应该是可以**安装**的。

  - 这里值的安装就像平时安装软件一样，我们需要制定**安装的位置**、**安装的目标**。

- 我们可以将构建的**库文件**进行安装，同时也可以把构建的**可执行文件**进行安装。

  - 关键字**TARGETS**：
    - **TARGETS**是**install()**命令的关键字，如果我们要**安装库**和**安装可执行文件**，那么就要使用这个关键字。

- **安装库文件**：

  - 通常来讲，我们在构建的时候，会把**子目录**构建成库，因此一般安装**子目录**的库：

    - 我们使用**set**设置一个**变量**，这个变量包含需要安装的**所有库**：

      - `set(install_libs MathFunctions test_compiler_flags)`
        - 虽然有**test_compiler_flags**这个接口库，但是并不会安装接口库，接口库只是提供了一系列的**规则和特性**。

    - 接下来，判断是否构建了**MathMysqrt库**，进而选择是否安装**MathMysqrt库**。

      - ```cmake
        if (TARGET MathMysqrt)
            list(APPEND install_libs MathMysqrt)
        endif()
        ```

      - 这里我们使用**TARGET关键字**来检测是否**构建**了**MathMysqrt库**。

        - 当然，也可以继续使用**编译标志USE_MYMATH**。

    - 接下来，执行**安装命令**：

      - ```cmake
        install(TARGETS ${install_libs} DESTINATION lib)
        ```

        - 这里面使用**TARGETS关键字**，表示安装的是**库**/**可执行文件**。
        - **DESTINATION关键字**后面接安装的路径：
          - 对于**Linux操作系统**来讲，通常安装在**usr/local/lib**中，因此路径是**lib**。

    - 库文件都有对外的接口头文件，因此我们需要安装接口头文件：

      - ```cmake
        set(install_includes MathFunctions.h)
        install(FILES ${install_includes} DESTINATION include)
        ```

      - 对于**Linux操作系统**来说，**接口头文件**通常安装在**usr/local/include**中，因此路径是**include**。

- **安装顶层的可执行文件**：

  - ```cmake
    install(TARGETS Test DESTINATION bin)
    ```

    - 对于**Linux操作系统**来讲，会将可执行文件安装在**usr/local/bin**中，因此路径是**bin**。

  - 通常来讲，我们还会安装可执行文件的**配置头文件**：

    - 当然，不安装配置头文件，可执行文件也是能够执行的。

    - ```cmake
      install(FILES "${PROJECT_BINARY_DIR}/TestConfig.h" DESTINATION include)
      ```

- **子目录CMakeLists.txt**：

  - ```cmake
    add_library(MathFunctions MathFunctions.cpp)
    
    target_include_directories(MathFunctions INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})
    option(USE_MYMATH "Use my sqrt functions" OFF)
    
    if (USE_MYMATH)
    target_compile_definitions(MathFunctions PRIVATE "USE_MYMATH")
    
    add_library(MathMysqrt mysqrt.cpp)
    target_link_libraries(MathMysqrt PUBLIC test_compiler_flags)
    
    target_link_libraries(MathFunctions PRIVATE MathMysqrt)
    
    endif()
    target_link_libraries(MathFunctions PUBLIC test_compiler_flags)
    
    set(install_libs MathFunctions test_compiler_flags)
    set(install_includes MathFunctions.h)
    
    if (TARGET MathMysqrt)
        list(APPEND install_libs MathMysqrt)
    endif()
    
    install(TARGETS ${install_libs} DESTINATION lib)
    install(FILES ${install_includes} DESTINATION include)
    ```

- **顶层CMakeLists.txt**：

  - ```cmake
    cmake_minimum_required(VERSION 3.15)
    
    project(Test VERSION 1.0)
    
    add_library(test_compiler_flags INTERFACE)
    target_compile_features(test_compiler_flags INTERFACE cxx_std_11)
    
    configure_file(TestConfig.h.in TestConfig.h)
    
    add_subdirectory(MathFunctions)
    
    add_executable(Test test.cpp)
    
    target_link_libraries(Test PUBLIC MathFunctions test_compiler_flags)
    
    target_include_directories(Test PUBLIC 
                               "${PROJECT_BINARY_DIR}"
    )
    
    install(TARGETS Test DESTINATION bin)
    install(FILES "${PROJECT_BINARY_DIR}/TestConfig.h" DESTINATION include)
    ```

- **安装指令**：

  - ```shell
    cmake --build .  #先构建
    sudo cmake --install .   #安装，需要管理员权限
    ```

